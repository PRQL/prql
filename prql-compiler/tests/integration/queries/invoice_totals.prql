# skip_mysql
# skip_mssql
from i=invoices
join ii=invoice_items [==invoice_id]
derive [
    month = s"substring(CAST({i.invoice_date} AS TEXT), 1, 7)",
    day = s"substring(CAST({i.invoice_date} AS TEXT), 1, 10)",
]
group [month, day] (
    aggregate [
        num_orders = s"COUNT(DISTINCT {i.invoice_id})",
        num_tracks = sum ii.quantity,
        total_price = sum (ii.unit_price * ii.quantity),
    ]
)
group [month] (
    sort day
    window expanding:true (
        derive [running_total_num_tracks = sum num_tracks]
    )
)
sort day
derive [num_tracks_last_week = lag 7 num_tracks]
select [
    month,
    day,
    num_orders = (num_orders | as int),
    num_tracks = (num_tracks | as int),
    total_price = (total_price | as float | round 2),
    running_total_num_tracks = (running_total_num_tracks | as int),
    num_tracks_last_week = (num_tracks_last_week | as int)
]
