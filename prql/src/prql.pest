// TODO:
// - Some rules are silent because we don't strictly need them, but that may be
//   too rushed — maybe we do actually want to know about e.g. comments in the
//   parse tree (and potentially put them into SQL comments)
// - Need to resolve how to handle "inline pipelines"; there is a rule here but
//   it's not used or tested. It's partly a language question — do those need to
//   start with `from`? How do these work in the midst of an `aggregate`
//   transform?
// - Currently there's no concept of what expressions are allowed. e.g. `1 + 1`
//   is valid, but so is `1 1`. Or `foo bar` is not parsed at this stage as
//   `foo` being a function and `bar` its argument — it's just parsed into a
//   sequence of idents. We could instead parse these explicitly, and then
//   disallow `1 1`.

WHITESPACE = _{ " " }
// Need to exclude # in strings (and maybe confirm whether this the syntax we want)
COMMENT = _{ "#" ~ (!NEWLINE ~ ANY) * }

query = { SOI ~ NEWLINE* ~ (( func_def | table | pipeline ) ~ ( NEWLINE+ | &EOI ))* ~ EOI }

func_def = { "func" ~ ident ~ func_def_params ~ "=" ~ expr }
// TODO: we could force the named parameters to follow the positional ones here.
// But pest requires some special handling because if `ident` is before
// `named_arg`, then `x:y` will be parsed as `x` and then error on `:y`.
// Possibly we can mandate whitespace to follow, but pest has annoying magic
// which removes whitespace in non-atomic rules, so we'd need to explore that.
func_def_params = { ( named_arg | ident )* }
table = { "table" ~ ident ~ "=" ~ "(" ~ NEWLINE* ~ pipeline ~ NEWLINE* ~ ")" }

pipe = _{ NEWLINE+ | "|" }
pipeline = { transformation ~ (pipe ~ transformation)* }

// `ident` is the transformation name.
transformation = { WHITESPACE* ~ ident ~ (named_arg | named_expr_simple)* }

// We don't model this in the same way as a pipeline, since the first term isn't
// a transformation. And it's somewhat related to our transformations being
// separate from our function calls (generally just `idents`) at the moment — we
// could unify them at some point.
inline_pipeline = { "(" ~ expr ~ (pipe ~ func_curry)+ ~ ")" }

ident = @{ ( ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "." | "_" )* ) }

// A central issue around the terms vs expr is that we want to be able to parse:
//   [foo bar + 1, 2]
// as:
// - foo bar + 1
//   - foo bar
//     - foo
//     - bar
//   - +
//   - 1
// - 2
// So this requires two non-silent rules:
// - A notion of list item that contains anything, including operators (but
//   not commas); e.g. `foo bar + 1`.
// - A notion of expr that aggregates things between operators, e.g. foo bar.
// So we call the list item `expr`, and the things between separators `terms`.
//
// We could have them be the same, but then we need logic in the parser to
// account for where the list item is in this parse tree
// - foo bar
//   - foo
//   - bar
// - +
// - 1
// - 2

// Adding the WHITESPACE is like making it a compound-atomic operator.
expr = !{ term ~ (operator ~ term)* }
expr_simple = !{ term_simple ~ (operator ~ term_simple)* }

func_call = { ident ~ (named_arg | term_simple)+ }
func_curry = { ident ~ (named_arg | term_simple)* }

term = _{ ( s_string | func_call | term_simple ) }
term_simple = _{ ( s_string | ident | parenthesized_expr | list | number | all | string_literal | inline_pipeline ) }
list = { "[" ~ NEWLINE? ~ named_expr ~ ("," ~ NEWLINE? ~ named_expr )* ~ ","? ~ NEWLINE? ~ "]" }
parenthesized_expr = _{ "(" ~ expr ~ ")" }

// TODO: escapes
// https://pest.rs/book/examples/rust/literals.html

// We need to have a non-silent rule which contains the quotes, because of
// https://github.com/pest-parser/pest/issues/583, and then when converting to
// AST, we only keep the `string` and discard the `string_literal` given it
// contains the quotes.
//
// TODO: I'm still a bit unclear how preceeding and trailing spaces are working
// -- it seems that inner spaces are included without an atomic operator (or
// with `ANY`), but prceeding & trailing spaces require both `ANY` _and_ an
// atomic operator. We have some rudimentary tests for these.

quote = _{ "\"" | "'" }
// PEEK refers to the opening quote; either `"` or `'`.
string = { ( !( "\\" | PEEK ) ~ ANY )+ }
string_literal = ${ PUSH(quote) ~ string ~ POP }

number = ${ ( ASCII_DIGIT | "." )+ }
all = { "*" }

// These separate idents from each other — i.e. `foo bar and baz` will parse into
// a) `foo bar`, b) `and`, c) `baz`, where `foo bar` is presumably a function call.
// TODO: review operators
operator = ${ "and" | "or" | "not" | "==" | "!=" | "=" | ">" | "<" | ">=" | "<=" | "+" | "-" | "*" | "/" | "%" }

s_string = ${ "s" ~ PUSH(quote) ~ ( s_string_string | ( "{" ~ expr ~ "}" ))* ~ POP }
s_string_string = { ( !( "\\" | PEEK | "{" ) ~ ANY )+ }

// For assigns and table aliases
named_expr = { (ident ~ "~")? ~ expr }
named_expr_simple = { (ident ~ "~")? ~ expr_simple }
// For function arguments
named_arg = { ident ~ ":" ~ term_simple }

// We no longer use this, but keeping here for reference, as it was a useful
// pattern for reducing nesting.
// // If possible, parse a single expr, otherwise parse a list of expr, adding a
// // layer. In cases like [a, b], we don't need `a` to be expr -> ident -> a;
// // only ident -> a; but in [a b], we do need an expr parent to discriminate it
// // from `[a, b]`.
// // Note that it's a bit complicated with `&( NEWLINE? ~ ("," | "]"))` — we want
// // to ensure there is a end to the expression, and we allow a NEWLINE to be between
// // the current expression and the ending token. (Feel free to change and see the
// // test diffs.)
// // single_expr_if_possible = _{ (( item ~ &( NEWLINE? ~ ("," | "]") ) ) | expr ) }
